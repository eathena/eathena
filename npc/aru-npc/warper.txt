//===== eAthena Script =======================================
//= Warp NPCs++
//===== By: ==================================================
//= eAthena team, with slight changes by Arutema
//===== Current Version: =====================================
//= 1.1
//===== Compatible With: =====================================
//= Any eAthena Version which supports the duplicate() function; 
//= kRO Sakray 2004-04-27 or newer
//===== Description: =========================================
//= Athena warp NPCs with Niflheim, Prontera Maze, Gonryun,
//= and Amatsu dungeon added.
//===== Additional Comments: =================================
//= Changed to the Comodo Kafra sprite everyone seems to like
//= Added cutins
//= Niflheim warp NPC has a warped sense of humor
//============================================================ 

function	script	warpra_menu	{
	next;
	menu "Towns",stadt,"Dungeons",dungeon,"Battlegrounds",guild;
	
	stadt:
	menu "Prontera",wprontera,
		"Aldebaran",waldebaran,
		"Geffen",wgeffen,
		"Payon",wpayon,
		"Alberta",walberta,
		"Amatsu",wamatsu,
		"Ayothaya",wayothaya,
		"Comodo",wcomodo,
		"Einbroch",wein,
		"Gonryun",wgonryun,
		"Izlude",wizlude,
		"Juno",wyuno,
		"Lighthalzen",wlhz,
		"Louyang",wlouyang,
		"Lutie",wxmas,
		"Morroc",wmorroc,
		"Niflheim",wnifl,
		"Umbala",wumbala;
	
	dungeon:
	menu "Amatsu Dungeon",dama,
		"Anthell",dant,
		"Ayothaya Labyrinth",dayo,
		"Bibilan Island",dbibilan,
		"Coal Mine (Dead Pit)",dcoal,
		"Culvert",dculvert,
		"Einbroch Mines",dein,
		"Glast Heim",dglast,
		"Hidden Temple",dmaze,
		"Magma Dungeon",dmagma,
		"Orc Dungeon",dorc,
		"Payon Cave",dpayon,
		"Pyramids",dpyramids,
		"Sphinx",dsphinx,
		"Sunken Ship",dsunken,
		"Turtle Island",dturtle;

	guild:
	menu "Valkyrie Realms",gprt,
		"Britoniah",ggef,
		"Chung-Rim",gpay,
		"Luina",galde;


//----------------Towns----------------\\
wlhz:
	warp "lighthalzen.gat",159,93;
	close;
wein:
	warp "einbroch.gat",231,190;
	close;
wayothaya:
	warp "ayothaya.gat",156,51;	
	close;
wprontera:
	set @warpx,149+rand(12);
	set @warpy,164+rand(21);
	warp "prontera.gat",	@warpx,	@warpy;	
	close;
wpayon:	
	set @warpx, 170+rand(5);
	set @warpy, 105+rand(5);
	warp "payon.gat",	@warpx,	@warpy;
	close;
wgeffen:
	set @warpx,115+rand(9);
	set @warpy,94+rand(9);
	warp "geffen.gat",	@warpx,	@warpy;
	close;
waldebaran:
	set @warpx,136+rand(7);
	set @warpy,112+rand(5);
	warp "aldebaran.gat",	@warpx,	@warpy;
	close;

wizlude:	warp "izlude.gat",	128,	111;	close;
wmorroc:	warp "morocc.gat",	159,	93;	close;
walberta:	warp "alberta.gat",	27,	236;	close;
wxmas:		warp "xmas.gat",	148,	131;	close;
wcomodo:   	warp "comodo.gat",	188,	161;    close;
wyuno:   	warp "yuno.gat",	158,	168;    close;
wamatsu:	warp "amatsu.gat",	197,	86;	close;
wgonryun:	warp "gonryun.gat",	150,    130; 	close;
wumbala:	warp "umbala.gat",	130,    130; 	close;
wlouyang:	warp "louyang.gat",	217,	106;	close;
wnifl:		warp "niflheim.gat",	193,	186;	close;

//----------------Dungeons----------------\\

dculvert:	warp "prt_fild05.gat",	273,	210;	close;
dpayon:		warp "pay_arche.gat",	43,	132;	close;
dcoal:		warp "mjolnir_02.gat",	81,	359;	close;
dglast:		warp "glast_01.gat",	368,	303;	close;
dorc:		warp "gef_fild10.gat",	70,	332;	close;
dsunken:	warp "alb2trea.gat",	75,	98;	close;
dbibilan:	warp "izlu2dun.gat",	106,	88;	close;
dant:		warp "moc_fild04.gat",	210,	328;	close;
dsphinx:	warp "moc_fild19.gat",	107,	100;	close;
dpyramids:	warp "moc_ruins.gat",	62,	162;	close;
dmagma:		warp "yuno_fild03.gat",	39,	140;	close;
dturtle:	warp "tur_dun01.gat",	149,	238;	close;
dmaze:		warp "prt_fild01.gat",	136,	363;	close;
dama:		warp "ama_fild01.gat",	174,	323;	close;
dayo:		warp "ayo_fild02.gat",	275,	150;	close;
dein:		warp "einbech.gat",	139,	247;	close;

gprt:		warp "prt_gld.gat",	0,	0;	close;
ggef:		warp "gef_fild13.gat",	0,	0;	close;
gpay:		warp "pay_gld.gat",	0,	0;	close;
galde:		warp "alde_gld.gat",	0,	0;	close;
}


-	script	Warpmaster::warpra	721,{
	set @image,rand(3);
	if (@image==0) cutin "kafra_07",2;
	if (@image==1) cutin "kkafra_07",2;
	if (@image==2) cutin "ukafra_07",2;
	mes "[Warpmaster]";
	mes "Hello,";
	mes "I can warp you to any Town and Dungeon!";
	mes "Were do you want to go?";
	callfunc "warpra_menu";
	close;
}

alberta.gat,31,240,4	duplicate(warpra)	Warpmaster	721
aldebaran.gat,145,118,4	duplicate(warpra)	Warpmaster	721
amatsu.gat,202,83,3	duplicate(warpra)	Warpmaster	721
comodo.gat,194,158,4	duplicate(warpra)	Warpmaster	721
geffen.gat,115,105,5	duplicate(warpra)	Warpmaster	721
izlude.gat,131,116,4	duplicate(warpra)	Warpmaster	721
xmas.gat,152,138,5	duplicate(warpra)	Warpmaster	721
morocc.gat,160,97,4	duplicate(warpra)	Warpmaster	721
payon.gat,179,107,4	duplicate(warpra)	Warpmaster	721
prontera.gat,147,170,5	duplicate(warpra)	Warpmaster	721
yuno.gat,167,176,4	duplicate(warpra)	Warpmaster	721
moc_fild04.gat,207,331,4	duplicate(warpra)	Warpmaster	721
izlu2dun.gat,104,82,0	duplicate(warpra)	Warpmaster	721
mjolnir_02.gat,85,363,4	duplicate(warpra)	Warpmaster	721
prt_fild05.gat,273,215,4	duplicate(warpra)	Warpmaster	721
glast_01.gat,370,308,4	duplicate(warpra)	Warpmaster	721
yuno_fild03.gat,37,135,4	duplicate(warpra)	Warpmaster	721
gef_fild10.gat,71,339,4	duplicate(warpra)	Warpmaster	721
pay_arche.gat,39,135,4	duplicate(warpra)	Warpmaster	721
moc_ruins.gat,64,166,4	duplicate(warpra)	Warpmaster	721
moc_fild19.gat,106,97,4	duplicate(warpra)	Warpmaster	721
alb2trea.gat,73,101,4	duplicate(warpra)	Warpmaster	721
tur_dun01.gat,148,239,4	duplicate(warpra)	Warpmaster	721
gonryun.gat,151,130,4	duplicate(warpra)	Warpmaster	721
louyang.gat,210,106,4	duplicate(warpra)	Warpmaster	721
umbala.gat,132,130,4	duplicate(warpra)	Warpmaster	721
prt_fild01.gat,140,364,4	duplicate(warpra)	Warpmaster	721
ama_fild01.gat,181,325,4	duplicate(warpra)	Warpmaster	721
ayothaya.gat,164,54,5	duplicate(warpra)	Warpmaster	721
ayo_fild02.gat,278,152,4	duplicate(warpra)	Warpmaster	721
einbroch.gat,237,195,3	duplicate(warpra)	Warpmaster	721
einbech.gat,132,252,5	duplicate(warpra)	Warpmaster	721
lighthalzen.gat,164,100,3	duplicate(warpra)	Warpmaster	721

niflheim.gat,190,177,3	script	Warpmaster	721,{
	cutin "kafra_07",2;
	if(nf_warpjoke == 2) goto L_NormalWarp;
	if(nf_warpjoke == 1) goto L_JustKidding;

	mes "[Warpmaster]";
	mes "^FF0000Kyahahahahaha!^000000";
	mes "Did you really think I'd let you leave?";
	set nf_warpjoke,1;
	next;
	cutin "kafra_07",255;
	close;

L_JustKidding:
	mes "[Warpmaster]";
	mes "^00FF00Teeheeheeheeheehee!^000000";
	mes "Just kidding.";
	mes "Now where do you wanna go?";
	set nf_warpjoke,2;
	callfunc "warpra_menu";
	close;

L_NormalWarp:
	mes "[Warpmaster]";
	mes "Hello,";
	mes "I can warp you to any Town and Dungeon!";
	mes "Were do you want to go?";
	callfunc "warpra_menu";
	close;
}
